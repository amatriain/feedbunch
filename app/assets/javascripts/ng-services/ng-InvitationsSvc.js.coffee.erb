########################################################
# AngularJS service to manage sending invitations to join Feedbunch to friends
########################################################

angular.module('feedbunch').service 'invitationsSvc',
['csrfTokenSvc', 'timerFlagSvc',
(csrfTokenSvc, timerFlagSvc)->

  # CSRF token for POST
  token = csrfTokenSvc.get_token()

  # Web worker to send invitations in a different thread.
  worker = new Worker '<%= asset_path 'workers/invitations_worker' %>'
  worker.onmessage = (e) ->
    if e.data.status == 200
      timerFlagSvc.start 'success_invite_friend'
    else if e.data.status == 202
      timerFlagSvc.start 'success_invitation_resend'
    else if e.data.status == 401 || e.data.status == 422
      $window.location.href = '/login'
    else if e.data.status == 403
      timerFlagSvc.start 'invite_friend_unauthorized'
    else if e.data.status == 409
      timerFlagSvc.start 'error_invited_user_exists'
    else if e.data.status == 400
      timerFlagSvc.start 'no_invitations_left'
    else
      timerFlagSvc.start 'error_sending_invitation'

  service =

    #---------------------------------------------
    # Send an invitation to join Feedbunch to a friend
    #---------------------------------------------
    send_invitation: (email)->
      # Email to send the invitation to
      if email
        worker.postMessage {token: token, email: email}

  return service
]